println """
*******************************************
 You are building SKCraft Launcher! 

 Output files will be in [subproject]/build/libs
*******************************************
"""

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
    }
}

ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simple config or poject.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.version}+${System.getenv("BUILD_NUMBER") ?: "SNAPSHOT"}"

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    group = 'com.skcraft'
    version = "${config.version}+${System.getenv("BUILD_NUMBER") ?: "SNAPSHOT"}"

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    if (JavaVersion.current().isJava8Compatible()) {
        // Java 8 turns on doclint which we fail
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task jsonFilter(type: Copy){
	from 'modpacks/launcher'
	into 'launcher/build'

    expand 'version':project.version
}
